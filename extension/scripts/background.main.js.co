($, window.player) <- require [\jquery \moefm/player]
S = localStorage

if 0 !== S.action_click?.indexOf('pop_')
    chrome.browserAction.setPopup do
        popup: ''
    chrome.browserAction.onClicked.addListener ->
        require [\akarin], (akarin)->
            akarin.poke!
else
    chrome.browserAction.setPopup do
        popup: 'popup.html'

$(window)bind \player_next_song, (event, song)->
    return if !S.song_notify
    n = chrome.extension.getViews {type:'notification'} .filter ->
        it.location.pathname=='/ntf_song.html'
    .0
    if !n
        ntf = webkitNotifications.createHTMLNotification \ntf_song.html
        ntf.show!

window <<<
    switchMode: ->
        (w) <-! chrome.windows.getLastFocused
        with w
            left = @left - screen.availLeft+ (@width - 375) / 2
            top = @top - screen.availTop + (@height - 130) / 2
            window.open (chrome.extension.getURL 'pop_switch_mode.html'), \com.mcfog.chrome.mmf.swich_mode, 
                "noresize=yes,width=380,height=130,top=#top,left=#left"

player.switchMode(localStorage.mode) if localStorage.mode
chrome.extension.onRequest.addListener (request, sender, sendResponse)->
    switch request.action
    case 'eval'
        data = request.data.data
        fn = eval '('+request.data.func+')'
        return (sendResponse fn? window)||{}
    default
        sendResponse {}
