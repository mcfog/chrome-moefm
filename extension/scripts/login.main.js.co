($) <- require [\jquery, \lib/jquery.sjtp.min, \lib/jquery.inputtip]
<- $

$ document.body .render ($ \#main_tpl), {}

$form = $ 'form.login'
$form .one \submit, ->
    $ '.info' .text '少女登录中...'
    $form.fadeTo \fast, 0.5
    me = @@callee
    <~ setTimeout
    username = $ 'input[name=username]' .val!
    password = $ 'input[name=password]' .val!
    $.ajax do
        url:'https://api.moefou.org/auth/xauth.json?consumer_key=com.mcfog.chrome.mmf'
        type: \POST
        dataType: \json
        data:
            username:username
            password:password
        async: false
    .success (rst)->
        if rst.error
            $ '.info' .text '登录失败'
            $form.fadeTo \fast, 1
            $form .one \submit,me
        else
            localStorage{username, password} = {username, password}

            t=2
            $ '<iframe>',
                src:'http://moe.fm/login?no_invitation=1&mmf_inject=1'
            .appendTo document.body .css do
                position: \absolute
                top:100000
                left:100000
            .load ->
                t:=t-1
                if t<=0
                    setTimeout ->
                        window.close!
                    chrome.extension.sendRequest do
                        action:\eval
                        data:""+(BG)->
                            BG.player.play!
                        , ->
            setTimeout location.reload, 5000
        void
    false
