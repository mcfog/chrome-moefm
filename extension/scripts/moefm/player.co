
($, playlist, u)<- define [\jquery, \moefm/playlist, \mc/util]
new
    mgr = new playlist.Manager playlist.random
    player = @
    
    @$audio = @song = null
    @playing = false
    @stop = ~>
        @$audio?.remove!
        @$audio = null
        @playing = false
        void

    getNextSong = ~>
        if @song = mgr.next!
            chrome.browserAction.setTitle do
                title: [@song.title, decodeURIComponent @song.wiki_title].join('\n')
        @song

    @play = ~>
        @stop!
        song = getNextSong!
        @$audio := $ '<audio>',
            src: song.url,
            controls: false,
            autoplay: true
        .appendTo document.body .addClass \fmplayer .bind \playing, ~>
            @playing = true
            void
        .bind \pause, ~>
            @playing = false
            void
        .bind 'ended emptied', ~>
            $.get 'http://moe.fm/ajax/log', 
                log_obj_type:'sub'
                log_type:'listen'
                obj_type:'song'
                obj_id:@song.sub_id
            @skip!
            void                
        .bind \loadstart, ~>
            u.badge '>_<', \orange
            void
        .bind \canplay, ~>
            u.badge '^_^', \blue
            void
        .data \begin, Date.now!
        void
        
    @pause = ~>
        if !@$audio
            return
        if @playing
            @$audio.0.pause!
        else
            @$audio.0.play!
        void

    @setVolumn = (v) ~>
        @$audio?.volume = v
        void

    @skip = ~>
        if !@$audio
            @play!
            return
        song = getNextSong!
        @$audio?.attr do
            src:song.url
        .data \begin, Date.now!
        void
    
    setTimeout ~>
        text = ''
        if @playing
            tdiff = @song.stream_length - @$audio.0.currentTime
            if tdiff < 60
                text = (parseInt tdiff)+''
            else
                text = (parseInt tdiff/60) + 'm'
        u.badge text, \blue if text
        setTimeout @@callee, 1000
    , 1000
#EOF
