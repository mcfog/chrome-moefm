($, user, _)<- define [\jquery, \moefm/user, \underscore]
PLIST = new
    fetch = ->
        rst = {info:{}, playlist:[]}
        $.ajax do
            url:it
            async: false
        .success !->
            if $.isArray it.playlist
                rst := it
            else
                user.do_login!
        rst
    @mode_random = ->
        fetch 'http://moe.fm/listen/playlist?share_buttons=1'
    _mk_mode = (url, shuffled = true)->
        ->
            me = @@callee
            me.next = url if !me.next
            rst = fetch me.next
            me.next = if rst.info.may_have_next then rst.info.next_url else false
            rst.playlist = _.shuffle rst.playlist if shuffled
            rst
    @mode_song = _mk_mode 'http://moe.fm/listen/playlist?share_buttons=1&fav=song'
    @mode_album = _mk_mode 'http://moe.fm/listen/playlist?share_buttons=1&fav=music'
    @mode_radio = _mk_mode 'http://moe.fm/listen/playlist?share_buttons=1&fav=radio'

    @mode_endlessEight = (list = false)->
        if list
            list
        else
            {info:{}, playlist:[@pool.playlist[@current]]}


    @Manager = class
        (@_mode, args=[])->
            @switchMode ...
        switchMode: !(@_mode, args=[])->
            @_fetch = ->
                PLIST["mode_#{@_mode}"].apply @, args
            @pool = @_fetch!
            @current = -1

        mode:~ ->@_mode
        next: ->
            @current += 1
            if @current >= @pool.playlist.length
                @pool = @_fetch!
                @current = 0
            @pool.playlist[@current]

        order: (song)->
            song = fetchSong song if typeof song is not \object
            return false if !song
            if @current >= 0
                @pool = {info:{}, playlist:[@pool.playlist[@current], song]}
                @current = 0
            else 
                @pool = {info:{}, playlist:[song]}
                @current = -1
            true
    #end class Manager
    fetchSong = (id)->
        id = parseInt id
        return null if !id
        {info,playlist} = fetch "http://moe.fm/listen/playlist?song=#id"
        return null if !info.is_target
        playlist.0
            
#EOF
