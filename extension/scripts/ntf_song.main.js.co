($, cpanel) <- require [\jquery \moefm/cpanel \lib/jquery.sjtp.min]
BG = chrome.extension.getBackgroundPage!

$body = $ document.body
$body.bind \newSong (event, song)->
    $('.playing')render tmpl_playing, {song}
    <-!setTimeout
    $('button.fav')toggleClass 'activated', song.fav_wiki is not null
    $('button.heart')toggleClass 'activated', song.fav_sub?.fav_type == 1
    $('button.trash')toggleClass 'activated', song.fav_sub?.fav_type == 2

newSong = (song)->
    if !song
        return
    $body.trigger \newSong [song]

$ window .bind \player_next_song, (event, song)->
    newSong song
newSong BG.player.song if BG.player.song

$buffered = $ '.progress>.buffered'
$played = $ '.progress>.played'
setTimeout ->
    setTimeout @@callee, 1000
    if !BG.player.playing
        $buffered.width 0
        $played.width 0
        return
    aud = BG.player.$audio.0
    {duration, buffered, played} = aud
    if buffered.length>0
        $buffered.width ((100 * (buffered.end 0) / duration)toFixed 1) + \%
    if played.length>0
        $played.width ((100 * (played.end 0) / duration)toFixed 1) + \%
,1000

cpanel $body


t = parseInt localStorage.song_notify
if t>0
    timeout = setTimeout window.close, t
    $(document.body)hover !->
        clearTimeout timeout
    , !->
        timeout := setTimeout window.close, t
